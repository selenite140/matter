# Copyright (c) 2021 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")
import("//build_overrides/openthread.gni")
import("${chip_root}/src/platform/device.gni")
import("//build_overrides/nxp_sdk.gni")
import("${nxp_sdk_build_root}/nxp_sdk.gni")
import("${chip_root}/src/platform/nxp/${nxp_platform}/args.gni")
import("${nxp_sdk_build_root}/${nxp_sdk_name}/rt_sdk.gni")
import("${nxp_sdk_build_root}/${nxp_sdk_name}/rt_executable.gni")

assert(current_os == "freertos")
assert(target_os == "freertos")
assert(nxp_platform == "rt/rt1060")

declare_args() {
  tcp_download = false
  enable_ota_provider = false
  wifi_connect = true
  # The 2 params below are used only if wifi_connect is true, otherwise they're unused.
  wifi_ssid = "my_ssid"
  wifi_password = "my_password"
}

rt1060_platform_dir = "${chip_root}/examples/platform/nxp/${nxp_platform}"

rt_executable("all_cluster_app") {
  output_name = "chip-rt1060-all-cluster-example"

  defines = [
    "CONFIG_RENDEZVOUS_MODE=7",
  ]

  include_dirs = [
    "../../common/main/include",
    "../../common/main",

    "${chip_root}/examples/all-clusters-app/all-clusters-common/include",
  ]

  sources = [
    "${chip_root}/examples/all-clusters-app/all-clusters-common/src/bridged-actions-stub.cpp",
    "${chip_root}/examples/all-clusters-app/all-clusters-common/src/binding-handler.cpp",
    "${chip_root}/examples/all-clusters-app/all-clusters-common/src/static-supported-modes-manager.cpp",
    "src/AppMatterButton.cpp",
    "../../common/main/AppTask.cpp",
    "../../common/main/CHIPDeviceManager.cpp",
    "../../common/main/ClusterManager.cpp",
    "../../common/main/DeviceCallbacks.cpp",
    "../../common/main/ZclCallbacks.cpp",
    "../../common/main/AppMatterCli.cpp",
    "../../common/main/include/CHIPDeviceManager.h",
    "../../common/main/include/ClusterManager.h",
    "../../common/main/include/DeviceCallbacks.h",
    "../../common/main/include/AppEvent.h",
    "../../common/main/include/AppTask.h",
    "../../common/main/main.cpp",
  ]

  deps = [
    "${nxp_sdk_build_root}:nxp_sdk",
    "${chip_root}/src/platform/nxp/${nxp_platform}:nxp_platform",
    "${chip_root}/examples/common/QRCode",
    "${chip_root}/examples/all-clusters-app/all-clusters-common",
    "${chip_root}/src/lib",
    "${chip_root}/examples/platform/nxp/common/app/support:freertos_memory_utils",
  ]

  if (chip_enable_ota_requestor) {
    sources += [
      "${chip_root}/examples/platform/nxp/common/OTARequestorInitiator.cpp",
      "${chip_root}/examples/platform/nxp/common/OTARequestorInitiator.h",
#Adding mcuboot files      
      "${chip_root}/examples/platform/nxp/common/mcuboot_app_support/mcuboot_app_support.c",
      "${chip_root}/examples/platform/nxp/common/mcuboot_app_support/mcuboot_app_support.h",
      "${chip_root}/examples/platform/nxp/common/mcuboot_app_support/flash_partitioning.h",
    ]
    include_dirs += [
      "${chip_root}/examples/platform/nxp/common",
      "${chip_root}/examples/platform/nxp/common/mcuboot_app_support",
    ]
  }

  if (chip_enable_openthread) {
    deps += [
      "${openthread_root}:libopenthread-cli-ftd",
      "${openthread_root}:libopenthread-ftd",
    ]
  }

  if (tcp_download)
  {
    defines += [
      "TCP_DOWNLOAD=1",
    ]

    if (wifi_connect)
    {
      defines += [
        "WIFI_CONNECT=1",
        "WIFI_SSID=\"${wifi_ssid}\"",
        "WIFI_PASSWORD=\"${wifi_password}\""
      ]
    }

    include_dirs += [
      "../../common/tcp_download_test/include",
    ]

    sources += [
      "../../common/tcp_download_test/source/TcpDownload.cpp",
    ]
  }

  if (enable_otw_k32w0)
  {
    defines += [
      "MAIN_TASK_SIZE = ((configSTACK_DEPTH_TYPE)8192 / sizeof(portSTACK_TYPE))"
    ]

   include_dirs += [
      "otw_uploader/include",
    ]

    sources += [
      "otw_uploader/source/ISPInterface.cpp",
      "otw_uploader/include/ISPInterface.h",
      "otw_uploader/include/ot-rcp-fw-placeholder.bin.h",
    ]
  }

  if (enable_ota_provider)
  {
    defines += [
      "ENABLE_OTA_PROVIDER=1",
    ]

    include_dirs += [
      "../../common/ota_provider/include",
      "../../common/ota_provider/ota-provider-common",
    ]

    sources += [
      "../../common/ota_provider/ota-provider-common/RTOTAProviderExample.cpp",
      "../../common/ota_provider/ota-provider-common/RTBdxOtaSender.cpp",
      "../../common/ota_provider/source/OTAProvider.cpp",
    ]

    deps += [
      "${chip_root}/src/app/server",
      "${chip_root}/src/protocols/bdx",
      "${chip_root}/src/lib/shell",
      "${chip_root}/third_party/jsoncpp",
      "${chip_root}/src/lib/shell:shell_core",
      "${chip_root}/src/lib/shell:shell",
    ]
  }

  # In case a dedicated assert function needs to be supported the flag sdk_fsl_assert_support should be set to false
  # The would add to the build a dedicated application assert implementation.
  if (!sdk_fsl_assert_support) {
    sources += [ "../../common/main/AppAssert.cpp" ]
  }

  cflags = [ "-Wconversion" ]

  ldscript = "${rt1060_platform_dir}/app/ldscripts/MIMXRT1062xxxxx_flexspi_nor.ld"

  inputs = [ ldscript ]

  ldflags = [ "-T" + rebase_path(ldscript, root_build_dir),
              "-fno-common",
              "-Wl,--defsym=__stack_size__=2048",
              "-ffreestanding",
              "-fno-builtin",
              "-mapcs",
              "-u qspiflash_config",
              "-u image_vector_table",
              "-u boot_data",
              "-u dcd_data",
              "-Wl,-print-memory-usage", ]

  if (enable_ota_provider)
  {
    # As the OTA file will be stored in the littlefs file system, it is required to increase the size of the file system
    # To be able to store an OTA file with a MAX size of 640K,
    # it recommended to size the file system with:
    # 161 sectors of 4K => reserved for the OTA file
    # 32 sectors of 4K => reserved for Matter/OT/BLE settings storage
    ldflags += [ "-Wl,--defsym=gNVMSectorCountLink_d=193" ]
  }

  if (chip_enable_ota_requestor)
  {
    # If OTA build flag is enabled,
    # we would need to reserve enough space for the bootloader (MCUBoot) 
    # MCUBoot requires 0x11000 Bytes to be reserved at the base of the flash 
    # Consequently, some sections will need to be shifted
    ldflags += [ "-Wl,--defsym=__mcu_boot_size__=0x11000" ]
  }

  output_dir = root_out_dir
}

group("rt1060") {
  deps = [ ":all_cluster_app" ]
}

group("default") {
  deps = [ ":rt1060" ]
}
